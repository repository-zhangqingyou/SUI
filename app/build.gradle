apply plugin: 'com.android.application'
apply from: '../bugly-tinker.gradle'

def time = "${new Date().format("yyyyMMdd")}"//
def codename = "1"//当日迭代号
android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.zqy.gamemange"
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true

        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }
        dexOptions {
            //避免打包时内存不足，导致一个dex不完整，合并的时候发现大小不一致而报错
            javaMaxHeapSize "4g"
        }
    }

    //***********************************多渠道构建***************************************
    flavorDimensions "channel"
    productFlavors {
        androidx_测试 {
            dimension "channel"
            applicationId "com.zqy.gamemange"
            versionCode 1
            versionName "1." + time + "." + codename
            manifestPlaceholders = [
                    APP_NAME      : "androidx_测试",
                    APP_ICON      : "@mipmap/ic_launcher",
                    PACKAGE_NAME  : applicationId,
                    JSHARE_PKGNAME: applicationId,
                    JPUSH_PKGNAME : applicationId,
                    JPUSH_APPKEY  : "f43583a474c09d91cd138e01", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL : "gamemange000", //渠道号（不能使用中文）
                    TENCENT_APPID : "101551231",//腾讯开放平台注册应用得到的 appId,哪怕用不到也得放在这,不然manifest会有merge错误
            ]
        }
    }

    signingConfigs {
        config {
            storeFile file('../zqy_key.jks')
            storePassword "147258369"
            keyAlias "zqy"
            keyPassword "147258369"
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
    }
    //解决 Invoke-customs are only supported starting with Android O (--min-api 26)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /*Gradle3.3 以上的方法*/
    applicationVariants.all { variant ->
        // 更新至Android Studio 3.3 gradle 4.10.1
        variant.outputs.all {
            //重命名输出apk文件名
            outputFileName = "${getName()}(${new Date().format("yyyyMMddHHmm")}_${variant.versionName}_${variant.versionCode}).apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation project(path: ':base_request')
    implementation project(path: ':base_utils')
    implementation project(path: ':base_ui')

    //集成全量更新和热更新一体的平台  注意： 升级SDK已经集成crash上报功能，已经集成Bugly的用户需要注释掉原来Bugly的jcenter库
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.5'
    //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0
    implementation 'com.tencent.bugly:nativecrashreport:3.7.471'
    //指定tinker依赖版本（注：应用升级1.3.5版本起，不再内置tinker）
    implementation('com.tencent.tinker:tinker-android-lib:1.9.14.7') {
        exclude group: 'androidx.appcompat'
    }
}